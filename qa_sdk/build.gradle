/*
 * *******************************************************************************
 * Copyright (C) QuantActions AG - All Rights Reserved
 * Unauthorized copying of this file, via any medium is strictly prohibited
 * Proprietary and confidential
 * Written by Enea Ceolini <enea.ceolini@quantactions.com>, August 2024
 * *******************************************************************************
 */
//file:noinspection HardCodedStringLiteral

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven-publish'
apply plugin: 'kotlinx-serialization'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'com.google.devtools.ksp'
apply plugin: 'dagger.hilt.android.plugin'

def versionName = '1.1.3-rc03'
def versionCode = '56'

android {
    compileSdk = 36

    defaultConfig {
        minSdkVersion 23
        targetSdk 36

        buildFeatures.buildConfig = true

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments.put("apiKey", project.properties["test.apiKey"])
        testInstrumentationRunnerArguments.put("testIdentityId", project.properties["test.identityId"])
        testInstrumentationRunnerArguments.put("testDeviceId", project.properties["test.deviceId"])
        testInstrumentationRunnerArguments.put("testPassword", project.properties["test.password"])
        testInstrumentationRunnerArguments.put("testParticipationId", project.properties["test.participationId"])
        testInstrumentationRunnerArguments.put("testStudyId", project.properties["test.studyId"])
        testInstrumentationRunnerArguments.put("QA_SAMPLE_PASSWORD", project.properties["qa_sample_password"])
        testInstrumentationRunnerArguments.put("QA_SAMPLE_ID", project.properties["qa_sample_id"])
        testInstrumentationRunnerArguments.put("QA_SAMPLE_PART_ID", project.properties["qa_sample_part_id"])
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField("String", "QA_UUID", "\"${project.properties["qa_uuid"]}\"")
        buildConfigField("String", "QA_API_ROUTE", "\"https://qa-apis.azure-api.net/tap/v1/\"")
        buildConfigField("String", "QA_SAMPLE_PASSWORD", "\"${project.properties["qa_sample_password"]}\"")
        buildConfigField("String", "QA_SAMPLE_ID", "\"${project.properties["qa_sample_id"]}\"")
        buildConfigField("String", "QA_SAMPLE_PART_ID", "\"${project.properties["qa_sample_part_id"]}\"")
        buildConfigField("String", "QA_VERIFICATION_CODE", "\"${project.properties["qa_verification_code"]}\"")

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("long", "VERSION_CODE", "${versionCode}")
            buildConfigField("String","VERSION_NAME","\"${versionName}\"")
        }
        debug {
            minifyEnabled false
            buildConfigField("long", "VERSION_CODE", "${versionCode}")
            buildConfigField("String","VERSION_NAME","\"${versionName}\"")
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES.txt', 'META-INF/LICENSE.txt', 'META-INF/NOTICE.txt', 'META-INF/NOTICE', 'META-INF/LICENSE', 'META-INF/DEPENDENCIES', 'META-INF/notice.txt', 'META-INF/license.txt', 'META-INF/dependencies.txt', 'META-INF/LGPL2.1']
        }
    }
    buildFeatures{
        buildConfig = true
    }

    compileOptions {
        coreLibraryDesugaringEnabled = true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
        freeCompilerArgs = ['-Xjvm-default=all']
    }

    sourceSets {
        // Adds exported schema location as test app assets.
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    namespace = 'com.quantactions.sdk'
}


dependencies {

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'

    implementation fileTree(dir: "libs", include: ["*.jar"])

    // EXTRA
    api 'com.android.volley:volley:1.2.1'
    api 'commons-io:commons-io:2.20.0'

    // ANDROIDX
    api 'androidx.appcompat:appcompat:1.7.1'
    api "androidx.preference:preference-ktx:1.2.1"
    api 'androidx.multidex:multidex:2.0.1'
    api "androidx.core:core-ktx:1.17.0"
    api 'com.google.android.gms:play-services-location:21.3.0'

    // WORK MANAGER
    api "androidx.work:work-runtime:2.10.3" // (Java)
    api "androidx.work:work-runtime-ktx:2.10.3" // Kotlin + coroutines
    // lifecycle
    api 'androidx.lifecycle:lifecycle-service:2.9.2'

    // FIREBASE & GOOGLE SERVICES
    api platform('com.google.firebase:firebase-bom:34.1.0')
    api 'com.google.firebase:firebase-crashlytics'
    api 'com.google.firebase:firebase-analytics'
    api 'com.google.firebase:firebase-messaging'

    // TEST
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.10.2'
    androidTestImplementation "androidx.arch.core:core-testing:2.2.0"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.10.2'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.10.2'

    api 'com.jakewharton.timber:timber:5.0.1'

    // NETWORK and ROOM
    api "androidx.room:room-runtime:2.7.2"
    ksp "androidx.room:room-compiler:2.7.2"
    api "net.zetetic:android-database-sqlcipher:4.5.4"
    api "androidx.room:room-ktx:2.7.2"
    androidTestImplementation "androidx.room:room-testing:2.7.2"
    api 'com.squareup.okhttp3:logging-interceptor:5.1.0'
    api 'com.squareup.retrofit2:retrofit:3.0.0'
    api 'com.squareup.retrofit2:converter-gson:3.0.0'
    api 'com.squareup.retrofit2:adapter-rxjava2:3.0.0'
    api 'io.github.hadiyarajesh.flower-retrofit:flower-retrofit:3.1.0'
    api 'com.squareup.moshi:moshi:1.15.2'
    api 'com.squareup.moshi:moshi-kotlin:1.15.2'
    api 'com.squareup.retrofit2:converter-moshi:3.0.0'
    ksp 'com.squareup.moshi:moshi-kotlin-codegen:1.15.2'

    api 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    api 'io.reactivex.rxjava2:rxandroid:2.1.1'
    api 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

    api 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.9.0'

    dokkaHtmlPlugin 'org.jetbrains.dokka:android-documentation-plugin:2.0.0'

    // UI for cognitive tests
    api("com.google.android.material:material:1.12.0")

    // Dependency Injection
    implementation "com.google.dagger:hilt-android:2.57" // Check for latest version
    ksp "com.google.dagger:hilt-compiler:2.57"
}


dokkaHtmlPartial.configure {
    dokkaSourceSets {
        configureEach {
            moduleName.set("SDK")
            includes.from("module.md")
            noAndroidSdkLink.set(false)
            includeNonPublic.set(false)
            skipEmptyPackages.set(true)
            reportUndocumented.set(false)
            skipDeprecated.set(true)
            suppressInheritedMembers.set(true)
            perPackageOption {
                matchingRegex.set(".*\\.SyncWorker*") // will match all .internal packages and sub-packages
                suppress.set(true)
            }
            perPackageOption {
                matchingRegex.set(""".*adapters*""") // will match all .internal packages and sub-packages
                suppress.set(true)
            }
            perPackageOption {
                matchingRegex.set(""".*workers*""") // will match all .internal packages and sub-packages
                suppress.set(true)
            }
            perPackageOption {
                matchingRegex.set(""".*model*""") // will match all .internal packages and sub-packages
                suppress.set(true)
            }
            perPackageOption {
                matchingRegex.set(""".*repository*""") // will match all .internal packages and sub-packages
                suppress.set(true)
            }
            perPackageOption {
                matchingRegex.set(""".*api*""") // will match all .internal packages and sub-packages
                suppress.set(true)
            }
            perPackageOption {
                matchingRegex.set(""".RelaunchWorker""") // will match all .internal packages and sub-packages
                suppress.set(true)
            }
        }
    }
}

dokkaHtml.configure {
    outputDirectory.set(file("../docs"))
    dokkaSourceSets {
        configureEach {
            moduleName.set("SDK")
            includes.from("module.md")
            noAndroidSdkLink.set(false)
            includeNonPublic.set(false)
            skipEmptyPackages.set(true)
            reportUndocumented.set(false)
            skipDeprecated.set(true)
            suppressInheritedMembers.set(true)
            perPackageOption {
                matchingRegex.set(".*\\.SyncWorker*") // will match all .internal packages and sub-packages
                suppress.set(true)
            }
            perPackageOption {
                matchingRegex.set(""".*adapters*""") // will match all .internal packages and sub-packages
                suppress.set(true)
            }
            perPackageOption {
                matchingRegex.set(""".*workers*""") // will match all .internal packages and sub-packages
                suppress.set(true)
            }
            perPackageOption {
                matchingRegex.set(""".*repository*""") // will match all .internal packages and sub-packages
                suppress.set(true)
            }
            perPackageOption {
                matchingRegex.set(""".*api*""") // will match all .internal packages and sub-packages
                suppress.set(true)
            }
            perPackageOption {
                matchingRegex.set(""".RelaunchWorker""") // will match all .internal packages and sub-packages
                suppress.set(true)
            }
        }
    }
}

tasks.register('javadoc', Javadoc) {
    excludes = ['**/*.kt'] // < ---- Exclude all kotlin files from javadoc file.
}

// publish the library to github packages

publishing {
    publications {
        bar(MavenPublication) {
            groupId = 'com.quantactions'
            artifactId = 'quantactions-android-sdk'
            version = "${versionName}"
            artifact("$buildDir/outputs/aar/qa_sdk-release.aar")
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each { dependency ->
                    if (dependency.group != null && dependency.name != null && dependency.version != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                        dependencyNode.appendNode('scope', 'compile')
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/QuantActions/quantactions-android-sdk")
            credentials {
                username = gprusr
                password = gprkey
            }
        }
    }
}
